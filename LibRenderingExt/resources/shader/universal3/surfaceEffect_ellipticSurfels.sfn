#version 140

/*
 * This file is part of the open source part of the
 * Platform for Algorithm Development and Rendering (PADrend).
 * Web page: http://www.padrend.de/
 * Copyright (C) 2014 Claudius JÃ¤hn <claudius@uni-paderborn.de>
 * Copyright (C) 2017 Sascha Brandt <myeti@mail.uni-paderborn.de>
 *
 * PADrend consists of an open source part and a proprietary part.
 * The open source part of PADrend is subject to the terms of the Mozilla
 * Public License, v. 2.0. You should have received a copy of the MPL along
 * with this library; see the file LICENSE. If not, you can obtain one at
 * http://mozilla.org/MPL/2.0/.
 */

// ------------------
// vs
#ifdef SG_VERTEX_SHADER

vec4 sg_getVertexColor();						//!	\see sgHelper.sfn
vec3 sg_getVertexTangent_ms(); //!	\see sgHelper.sfn
vec4 sg_modelToCamera(in vec4 hms); //!	\see sgHelper.sfn
vec4 sg_cameraToClipping(in vec4 hcs);  //!	\see sgHelper.sfn
vec4 sg_clippingToCamera(in vec4 hcs); //!	\see sgHelper.sfn
float sg_getPointSize(); //!	\see sgHelper.sfn
vec4 sg_getViewport(); //!	\see sgHelper.sfn

struct VertexProperties {
	vec3 position_cs, normal_cs;
	float pointSize;
};

out vec3 var_tangent_cs;
out vec3 var_cotangent_cs;
out float surfelRadius;

//! \see main.sfn
void provideSurfaceEffectVars(in VertexProperties vert){

	/*vec3 tangent_ms = sg_getVertexTangent_ms();
	if(length(tangent_ms) < 1) {
		var_tangent_cs = normalize(dot(vert.normal_cs,vec3(0,1,0)) > 0 ? cross(vert.normal_cs,vec3(0,1,0)) : cross(vert.normal_cs,vec3(1,0,0)));
	} else {
		var_tangent_cs = normalize(sg_modelToCamera(vec4(tangent_ms, 0.0)).xyz);
	}*/
	
	var_tangent_cs = normalize(dot(vert.normal_cs,vec3(0,1,0)) > 0 ? cross(vert.normal_cs,vec3(0,1,0)) : cross(vert.normal_cs,vec3(1,0,0)));
	var_cotangent_cs = normalize(cross(vert.normal_cs, var_tangent_cs));
	
	vec4 viewport = sg_getViewport();
	vec4 center_cs = vec4(vert.position_cs,1.0);
	vec4 pos_clip = sg_cameraToClipping(center_cs);
	pos_clip = vec4(pos_clip.xyz / pos_clip.w, 1.0);		
	vec4 p2_clip = pos_clip + vec4(sg_getPointSize() / viewport.z, 0, 0, 0);
	vec4 p2_cs = sg_clippingToCamera(p2_clip);
	p2_cs = vec4(p2_cs.xyz / p2_cs.w, 1);
	surfelRadius = distance(center_cs, p2_cs);
}

#endif
// ------------------
// fs
#ifdef SG_FRAGMENT_SHADER

vec4 sg_cameraToClipping(in vec4 hcs);  //!	\see sgHelper.sfn
vec4 sg_clippingToCamera(in vec4 hcs); //!	\see sgHelper.sfn
float sg_getPointSize(); //!	\see sgHelper.sfn
vec4 sg_getViewport(); //!	\see sgHelper.sfn

struct CompositeColor {
	vec4 ambient, diffuse, specular;
};

struct SurfaceProperties {
	vec3 position_cs, normal_cs, tangent_cs;
	vec4 ambient, diffuse, specular, emission;
	float shininess;
};

in vec3 var_tangent_cs;
in vec3 var_cotangent_cs;
in float surfelRadius;

uniform bool renderSurfels;

//! \see main.sfn
void addSurfaceEffects(inout SurfaceProperties surface) {
	if(renderSurfels) {		
		vec4 viewport = sg_getViewport();
				
		// transform fragment coordinates to clipping space [-1,1]x[-1,1]x[-1,1]
		vec4 frag_clip = vec4((gl_FragCoord.xy - viewport.xy) / viewport.zw * 2.0 - 1.0, -1.0, 1.0);
		// transform fragment to camera space
		vec4 frag_cs = sg_clippingToCamera(frag_clip);
		// correction of homogeneous coordinates
		frag_cs.xyz /= frag_cs.w; frag_cs.w = 1.0;

		// project fragment to surfel disc
	  vec3 projected_frag_cs = frag_cs.xyz * dot(surface.position_cs, surface.normal_cs) / dot(frag_cs.xyz, surface.normal_cs);
		// get projected fragment position relative to surfel origin
	  vec3 rel_frag_cs = projected_frag_cs - surface.position_cs;

		// get surfel tangent
		vec3 tangent_cs = normalize(var_tangent_cs);
		// get surfel co-tangent
		vec3 cotangent_cs = normalize(var_cotangent_cs);
		// get fragment coordinate in local surfel space
	  vec2 frag_local = vec2(dot(tangent_cs, rel_frag_cs), dot(cotangent_cs, rel_frag_cs));
		
		// discard fragment if the projected fragment lies outside of the surfel disc
		if(length(frag_local) > surfelRadius )
			discard;		
	}
}

#endif
// ------------------
