#version 430

/*
 * This file is part of the open source part of the
 * Platform for Algorithm Development and Rendering (PADrend).
 * Web page: http://www.padrend.de/
 * Copyright (C) 2013 Claudius JÃ¤hn <claudius@uni-paderborn.de>
 * Copyright (C) 2017-2018 Sascha Brandt <sascha@brandt.graphics>
 * 
 * PADrend consists of an open source part and a proprietary part.
 * The open source part of PADrend is subject to the terms of the Mozilla
 * Public License, v. 2.0. You should have received a copy of the MPL along
 * with this library; see the file LICENSE. If not, you can obtain one at
 * http://mozilla.org/MPL/2.0/.
 */

// ------------------------------------------------------------------------
// vs
#ifdef SG_VERTEX_SHADER

layout(location=4) in vec2 sg_TexCoord0;
layout(location=5) in vec2 sg_TexCoord1;

out vec4 var_vertexColor;
out vec2 var_texCoord0, var_texCoord1;
out vec3 var_tangent_cs, var_bitangent_cs;	// normal mapping

struct VertexProperties {
	vec3 position_cs, normal_cs;
	float pointSize;
};
vec4 sg_getVertexColor();							//!	\see sgHelper.sfn
vec3 sg_getVertexNormal_ms();						//!	\see sgHelper.sfn
vec3 sg_getVertexTangent_ms();						//!	\see sgHelper.sfn
vec4 sg_modelToCamera(in vec4 hms);					//!	\see sgHelper.sfn

//! \see main.sfn
void provideSurfaceVars(in VertexProperties vec){
	var_vertexColor = sg_getVertexColor();
	var_texCoord0 = sg_TexCoord0;
	var_texCoord1 = sg_TexCoord1;
	vec3 tangent_ms = sg_getVertexTangent_ms();
	var_tangent_cs = sg_modelToCamera( vec4(tangent_ms,0.0) ).xyz;
	var_bitangent_cs = sg_modelToCamera( vec4(cross(sg_getVertexNormal_ms(), tangent_ms),0.0) ).xyz;
}

#endif

// ------------------------------------------------------------------------
// fs
#ifdef SG_FRAGMENT_SHADER

in vec4 var_vertexColor;
in vec2 var_texCoord0, var_texCoord1;
in vec3 var_tangent_cs, var_bitangent_cs;	// normal mapping

layout(binding=0) uniform sampler2D sg_texture0;
layout(binding=1) uniform sampler2D sg_texture1;
layout(binding=2) uniform sampler2D sg_specularMap;
layout(binding=3) uniform sampler2D sg_normalMap;
layout(binding=4) uniform sampler2D sg_emissionMap;

uniform bool sg_specularMappingEnabled, sg_normalMappingEnabled;
uniform bool sg_emissionMappingEnabled;

layout(std140, binding=4) uniform TextureSetData {
	bool sg_textureEnabled[16];
};

struct SurfaceProperties {
	vec3 position_cs, normal_cs, tangent_cs;
	vec4 ambient, diffuse, specular, emission;
	float shininess;
};

void initSurfaceColor_AmDiSp(inout SurfaceProperties surface,in vec4 c);		//!	\see sgHelper.sfn
void sg_initSurfaceFromSGMaterial(inout SurfaceProperties surface);				//!	\see sgHelper.sfn
bool sg_isMaterialEnabled();													//!	\see sgHelper.sfn

//! [fs,vs] Multiply a surface's ambient, diffuse and specular value.
void multSurfaceColor_AmDi(inout SurfaceProperties surface,in vec4 c){
	surface.ambient *= c;
	surface.diffuse *= c;
}

//! \see main.sfn
void calcSurfaceProperties(inout SurfaceProperties surface){

	// material
	if(sg_isMaterialEnabled()){
		sg_initSurfaceFromSGMaterial(surface);
		surface.emission.a = 0.0;
	}else{
		initSurfaceColor_AmDiSp(surface,var_vertexColor);
		surface.emission = vec4(0.0);
		//surface.ambient = vec4(0.0);
		surface.specular = vec4(0.0);
		surface.shininess = 0.0;
	}
	// texture
	if(sg_textureEnabled[0])
		multSurfaceColor_AmDi(surface,texture2D(sg_texture0, var_texCoord0));
	if(sg_textureEnabled[1])
		multSurfaceColor_AmDi(surface,texture2D(sg_texture1, var_texCoord1));

	if(sg_specularMappingEnabled) {
		surface.specular = texture2D(sg_specularMap, var_texCoord0);
		surface.shininess *= surface.specular.a;
		//surface.ambient += surface.specular;
	}
		
	if(sg_normalMappingEnabled){
		vec3 esTangent = normalize(var_tangent_cs);
		vec3 esBitangent = normalize(var_bitangent_cs);

		// Calculate eye->tangent space matrix
		mat3 tbnMat = mat3( esTangent, esBitangent, surface.normal_cs );
		vec3 tsNormal = texture2D(sg_normalMap,var_texCoord0).xyz - vec3(0.5,0.5,0.5);
		
		surface.normal_cs = normalize(tbnMat * tsNormal);
	}
	
	if(sg_emissionMappingEnabled) {
		surface.emission *= texture2D(sg_emissionMap, var_texCoord0);
	}
}

#endif
